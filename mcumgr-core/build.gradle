/*
 * Copyright (c) 2017-2018 Runtime Inc.
 * Copyright (c) Intellinium SAS, 2014-2021
 * Copyright (c) 2021, Nordic Semiconductor
 *
 * SPDX-License-Identifier: Apache-2.0
 */
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdk 35
    namespace "io.runtime.mcumgr"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 35
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'mcumgr-core-proguard-rules.pro'
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }

    compileOptions {
        // for now and foreseeable future we intentionally set the build system to emit bytecode that is compatible with
        // java11 so as to ensure that we don't break the "classic xamarin (mono)" toolchain for C# android-java-bindings
        // which employs an outdated version of r8 that can only handle java11 bytecode
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlin {
        compilerOptions {
            // for now and foreseeable future we intentionally set the build system to emit bytecode that is compatible with
            // java11 so as to ensure that we don't break the "classic xamarin (mono)" toolchain for C# android-java-bindings
            // which employs an outdated version of r8 that can only handle java11 bytecode
            jvmTarget = JvmTarget.JVM_11
        }
    }
}

dependencies {
    // Annotations
    implementation 'org.jetbrains:annotations:26.0.1'

    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.16'

    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.1'

    // Import CBOR parser - version 2.14+ requires Android 8
    // See: https://github.com/NordicSemiconductor/Android-nRF-Connect-Device-Manager/issues/135
    //noinspection GradleDependency
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.13.5' // don't update
    //noinspection GradleDependency
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.5' // don't update
    //noinspection GradleDependency
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.5' // don't update

    // Test
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.2'
}

// === Maven Central configuration ===
// The following file exists only when Android BLE Library project is opened, but not
// when the module is loaded to a different project.
if (rootProject.file('gradle/publish-module.gradle').exists()) {
    ext {
        POM_ARTIFACT_ID = 'mcumgr-core'
        POM_NAME = 'Mcu Manager Core'
        POM_PACKAGING = 'aar'
    }
    apply from: rootProject.file('gradle/publish-module.gradle')
}

// Quick fix for GitHub Actions issue:
// Reason: Task ':mcumgr-core:generateMetadataFileForReleasePublication' uses this output of task
// ':mcumgr-core:androidSourcesJar' without declaring an explicit or implicit dependency.
// This can lead to incorrect results being produced, depending on what order the tasks are executed.
afterEvaluate {
    generateMetadataFileForReleasePublication.dependsOn androidSourcesJar
}
