/*
 * Copyright (c) 2017-2018 Runtime Inc.
 * Copyright (c) Intellinium SAS, 2014-2021
 * Copyright (c) 2021, Nordic Semiconductor
 *
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdk 35
    namespace "io.runtime.mcumgr.ble"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 35
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'mcumgr-ble-proguard-rules.pro'
        }
    }

    compileOptions {
        // for now and foreseeable future we intentionally set the build system to emit bytecode that is compatible with
        // java11 so as to ensure that we don't break the "classic xamarin (mono)" toolchain for C# android-java-bindings
        // which employs an outdated version of r8 that can only handle java11 bytecode
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        // for now and foreseeable future we intentionally set the build system to emit bytecode that is compatible with
        // java11 so as to ensure that we don't break the "classic xamarin (mono)" toolchain for C# android-java-bindings
        // which employs an outdated version of r8 that can only handle java11 bytecode
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

dependencies {
    // Import mcumgr-core
    api project(':mcumgr-core')

    // Import the BLE Library
    api 'no.nordicsemi.android:ble:2.9.0'

    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.16'

    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

// === Maven Central configuration ===
// The following file exists only when Android BLE Library project is opened, but not
// when the module is loaded to a different project.
if (rootProject.file('gradle/publish-module.gradle').exists()) {
    ext {
        POM_ARTIFACT_ID = 'mcumgr-ble'
        POM_NAME = 'Mcu Manager BLE Transport'
        POM_PACKAGING = 'aar'
    }
    apply from: rootProject.file('gradle/publish-module.gradle')
}

// Quick fix for GitHub Actions issue:
// Reason: Task ':mcumgr-ble:generateMetadataFileForReleasePublication' uses this output of task
// ':mcumgr-ble:androidSourcesJar' without declaring an explicit or implicit dependency.
// This can lead to incorrect results being produced, depending on what order the tasks are executed.
afterEvaluate {
    generateMetadataFileForReleasePublication.dependsOn androidSourcesJar
}
